# SPDX-License-Identifier: GPL-2.0-or-later
#
# Blinkinlabs JTAG_Hat - Remix by Michael Schwingen
#
# https://github.com/blinkinlabs/jtag_hat
# https://github.com/mschwingen/hardware/jtag_hat
#
# Adapter speed calibration is computed from cpufreq/scaling_max_freq.
# Adjusts automatically if CPU is overclocked.
#

adapter driver bcm2835gpio

proc read_file { name } {
	if {[catch {open $name r} fd]} {
		return ""
	}
	set result [read $fd]
	close $fd
	return $result
}

proc measure_clock {} {
	set result [exec vcgencmd measure_clock arm]
	set clock_hz [lindex [split $result "="] 1]
	expr { $clock_hz / 1000 }
}

proc get_max_cpu_clock { default } {
	set clock [read_file /sys/devices/system/cpu/cpu0/cpufreq/scaling_max_freq]
	if { $clock > 100000 } {
		return $clock
	}

	# cpufreq not available. As the last resort try Broadcom's proprietary utility
	if {![catch measure_clock clock] && $clock > 100000} {
		return $clock
	}

	echo "WARNING: Host CPU clock unknown."
	echo "WARNING: Using the highest possible value $default kHz as a safe default."
	echo "WARNING: Expect JTAG/SWD clock significantly slower than requested."

	return $default
}

set compat [read_file /proc/device-tree/compatible]
set clocks_per_timing_loop 4

if {[string match *bcm2711* $compat]} {
	set speed_offset 52
} elseif {[string match *bcm2837* $compat] || [string match *bcm2710* $compat]} {
	set speed_offset 34
} elseif {[string match *bcm2836* $compat] || [string match *bcm2709* $compat]} {
	set speed_offset 36
} elseif {[string match *bcm2835* $compat] || [string match *bcm2708* $compat]} {
	set clocks_per_timing_loop 6
	set speed_offset 32
} else {
	set speed_offset 32
	echo "WARNING: Unknown type of the host SoC. Expect JTAG/SWD clock slower than requested."
}

set clock [get_max_cpu_clock 2000000]
set speed_coeff [expr { $clock / $clocks_per_timing_loop }]


# Transition delay calculation: SPEED_COEFF/khz - SPEED_OFFSET
# The coefficients depend on system clock and CPU frequency scaling.
bcm2835gpio speed_coeffs $speed_coeff $speed_offset

echo "bcm2835gpio speed_coeffs $speed_coeff $speed_offset"

#bcm2835gpio peripheral_base 0x20000000  # RPi1
#bcm2835gpio peripheral_base 0x3F000000  # RPi2/3
#bcm2835gpio peripheral_base 0xfe000000  # RPi4

# Each of the JTAG lines need a gpio number set: tck tms tdi tdo
# Header pin numbers: 23 24 19 21
adapter gpio tck -chip 0 11
adapter gpio tms -chip 0 8
adapter gpio tdi -chip 0 10
adapter gpio tdo -chip 0 9

# Each of the SWD lines need a gpio number set: swclk swdio
# Header pin numbers: 23 24
adapter gpio swclk -chip 0 11
adapter gpio swdio -chip 0 8
# Direction pin for SWDIO level shifting buffer
adapter gpio swdio_dir -chip 0 25

# If you define trst or srst, use appropriate reset_config
# Header pin numbers: TRST - 18, SRST - 15
adapter gpio trst -chip 0 24
adapter gpio srst -chip 0 22

# or if you have both connected
reset_config trst_and_srst
